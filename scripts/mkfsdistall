#! /bin/tcsh

#
# mkfsdistall
#
# Purpose: creates a full set of FreeSurfer snapshot tarballs in the
# directory specified on the command-line. It takes a long time to
# work and requires about 3G of disk space.
#
# TO MAKE THE DISTRO
#
# mkfsdistall <freesurfer_dir> <dest_dir>
# i.e. mkfsdistall /space/lyon/1/fsdev/freesurfer_dev ~/work
#
# Will create ~/work/freesurfer-DATE with tarballs inside, as well as
# a copy of freesurfer/.
#
# Make sure that you are in a scripts dir where mkfsdist and
# mkfssnapshot are as well, or that your DEV environment variable is
# set to a valid dev/ environment with those scripts in #DEV/scripts.
#
# Original Author: REPLACE_WITH_FULL_NAME_OF_CREATING_AUTHOR
# CVS Revision Info:
#    $Author: nicks $
#    $Date: 2011/03/02 20:16:39 $
#    $Revision: 1.6 $
#
# Copyright Â© 2011 The General Hospital Corporation (Boston, MA) "MGH"
#
# Terms and conditions for use, reproduction, distribution and contribution
# are found in the 'FreeSurfer Software License Agreement' contained
# in the file 'LICENSE' found in the FreeSurfer distribution, and here:
#
# https://surfer.nmr.mgh.harvard.edu/fswiki/FreeSurferSoftwareLicense
#
# Reporting: freesurfer@nmr.mgh.harvard.edu
#
#


if ( $#argv < 2 ) then
    echo "USAGE: mkfsdistall freesurfer_dir dest_dir [options]"
    echo ""
    echo "freesurfer_dir should be something like freesurfer_dev/."
    echo ""
    echo "A directory called freesurfer-DATE will be created in the"
    echo "dest_dir. It needs about 3 gigs of space."
    echo ""
    echo "Options:"
    echo "  --skip-copy, -s: Don't do the copy portion. assumes that "
    echo "                   dest_dir/freesurfer exists."
    echo "  --use-date, -d DATE: Don't use the current date for naming"
    echo "                       purposes but use DATE instead. This does"
    echo "                       not use an old version, it's just for"
    echo "                       continuing from a previous run."
    echo "                       Use format yyyymmdd."
    echo ""
    echo "Ex: mkfsdistall /space/lyon/1/fsdev/freesurfer_dev ~/work"
    echo ""
    echo "Ex: mkfsdistall /space/lyon/1/fsdev/freesurfer_dev ~/work \ "
    echo "    --skip-copy -d 20030514"
    exit 1
endif

set FREESURFER_DIR = $1
set TOP_DEST_DIR = $2

set DO_COPY = 1

# Our date string, something like 20030721.
set DATE = "`date '+%Y%m%d'`"

set narg = 0
while ($narg < $#argv)
    set option = $argv[$narg]
    echo "narg = $narg option = $option"
    switch ("$option")
    case "--skip-copy":
    case "-s":
	set DO_COPY = 0
	breaksw
    case "--use-date":
    case "-d":
	@ noption = $narg + 1
	set DATE = $argv[$noption]
	breaksw
    endsw
    @ narg ++
end

# Make sure the directories exist.
if(! -er $FREESURFER_DIR) then
  echo "ERROR: cannot find $FREESURFER_DIR"
  exit 1;
endif
if(! -ew $TOP_DEST_DIR) then
  echo "ERROR: cannot find $TOP_DEST_DIR"
  exit 1;
endif



# Make sure we have the mkfs* scripts for making the distro and
# snapshot below. If not, exit.
set MKFSDIST = $PWD/mkfsdist
which $MKFSDIST > /dev/null
if ( $? != 0 ) then
    set MKFSDIST = $DEV/scripts/mkfsdist
    which $MKFSDIST  > /dev/null
    if ( $? != 0 ) then
	echo "Couldn't find mkfsdist. Please add to path."
	exit 1
    endif
endif
set MKFSSNAPSHOT = $PWD/mkfssnapshot
which $MKFSSNAPSHOT > /dev/null
if ( $? != 0 ) then
    set MKFSSNAPSHOT = $DEV/scripts/mkfssnapshot
    which $MKFSSNAPSHOT  > /dev/null
    if ( $? != 0 ) then
	echo "Couldn't find mkfssnapshot. Please add to path."
	exit 1
    endif
endif

# Say which scripts we're using.
echo mkfsdist = $MKFSDIST
echo mkfssnapshot = $MKFSSNAPSHOT


# First create this directory tree.
# fsdistro-DATE
#   freesurfer
#   Linux
#     update
#       freesurfer-DATE
#     full
#       freesurfer-DATE
#   Solaris
#     ...
#   Darwin
#     ...

echo Making directory tree...

set DISTRO_DIR = $TOP_DEST_DIR/fsdistro-$DATE

mkdir -p $DISTRO_DIR
mkdir -p $DISTRO_DIR/freesurfer

foreach OS (Linux Darwin Solaris)
    mkdir -p $DISTRO_DIR/$OS
    mkdir -p $DISTRO_DIR/$OS/update
    mkdir -p $DISTRO_DIR/$OS/update/freesurfer-$DATE
    mkdir -p $DISTRO_DIR/$OS/full
    mkdir -p $DISTRO_DIR/$OS/full/freesurfer-$DATE
end



# Build the exclude file and copy freesurfer/. The exclude file keeps
# us from copying old backup files and other stuff.
if ( $DO_COPY ) then
    echo Building exclude file...

    set EXCLUDE_FILE = /tmp/copyfreesurfer-exclude-$OS-$DATE
    rm -f $EXCLUDE_FILE

    pushd $FREESURFER_DIR > /dev/null
    cd ..

    set FREESURFER_DIR_REL = `basename $FREESURFER_DIR`

    find $FREESURFER_DIR_REL -name \*.03\* >> $EXCLUDE_FILE
    find $FREESURFER_DIR_REL -name \*.02\* >> $EXCLUDE_FILE
    find $FREESURFER_DIR_REL -name \*.01\* >> $EXCLUDE_FILE
    find $FREESURFER_DIR_REL -name \*~ >> $EXCLUDE_FILE
    find $FREESURFER_DIR_REL -name \#\*\# >> $EXCLUDE_FILE
    find $FREESURFER_DIR_REL -name \*.bu >> $EXCLUDE_FILE
    find $FREESURFER_DIR_REL -name \*.bak >> $EXCLUDE_FILE
    find $FREESURFER_DIR_REL -name \*.log >> $EXCLUDE_FILE
    find $FREESURFER_DIR_REL -name \*.old >> $EXCLUDE_FILE
    find $FREESURFER_DIR_REL -name .xdebug\* >> $EXCLUDE_FILE

    # Copy the freesurfer directory given into the freesurfer/ part above.
    echo Copying $FREESURFER_DIR to $DISTRO_DIR/freesurfer...

    rsync --recursive --copy-links --exclude-from $EXCLUDE_FILE $FREESURFER_DIR/ $DISTRO_DIR/freesurfer

    rm -f $EXCLUDE_FILE
else
    if ( ! -e $DISTRO_DIR/freesurfer ) then
	echo "$DISTRO_DIR/freesurfer doesn't not exist; run "
	echo "without --skip-copy option to create it."
	exit 1
    endif
endif

# Put a text file with this date in the freesurfer directory.
touch $DISTRO_DIR/freesurfer/$DATE

# For each OS, run the mkfsdit and mkfssnapshot scripts. Tar each
# result and put the tarball in the fsdistro-DATE directory.
echo Running mkfs scripts...
foreach OS (Linux Darwin Solaris)

pushd $DISTRO_DIR/$OS/update
$MKFSSNAPSHOT $DISTRO_DIR/freesurfer $DISTRO_DIR/$OS/update/freesurfer-$DATE $OS
tar czf $DISTRO_DIR/freesurfer-$DATE-$OS-Update.tar.gz freesurfer-$DATE


pushd $DISTRO_DIR/$OS/full
$MKFSDIST $DISTRO_DIR/freesurfer $DISTRO_DIR/$OS/full/freesurfer-$DATE $OS
tar czf $DISTRO_DIR/freesurfer-$DATE-$OS.tar.gz freesurfer-$DATE

end
